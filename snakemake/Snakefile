from pathlib import Path
import json
import yaml



with open("configs/config_snakemake.yaml", "r", encoding="utf-8") as f:
    config_snakemake = yaml.load(f, Loader=yaml.FullLoader)


#define parameters , keep them in list as it iterates, multiple input can be included. 
regions = config_snakemake["study_region_name"]
technologies = config_snakemake["technologies"]
scenario= config_snakemake["scenario"]
weather_years = config_snakemake["weather_years"]

def logpath(region, filename):
    return Path("data") / region / "snakemake_log" / filename

# Create directories for each region and snakemake log
for r in regions:
    Path(f"data/{r}/snakemake_log").mkdir(parents=True, exist_ok=True)

"""script to run in terminal
The snakemake workflow will parallelize (--cores > 1) everything else but the spatial_data_prep script. 
The resources are limited to openeo cause the api is throwing problems with parallelization. 
    use thi line ---->  snakemake --cores 4 --resources openeo_req=1
to use a specific Snakefile, use the --snakefile option
    use this line ----> snakemake --snakefile snakemake/Snakefile_short_short --cores 4 --resources openeo_req=1
"""
# ---- Stage configuration ----
stages = config_snakemake.get("stages", {})

# ---- Create the global functions ----
globals().update({
    f"DO_{k.upper()}": v
    for k, v in stages.items()
})


# ---- Compact includes ----
for stage_name, enabled in stages.items():
    if enabled:
        include: f"rules/{stage_name}.smk"


# ---- Build 'all' targets (explicit form for clarity) ----
ALL_TARGETS = []

if DO_SPATIAL_DATA_PREP:
    ALL_TARGETS += expand(
        logpath("{region}", "spatial_data_prep.done"),
        region=regions,
    )

if DO_EXCLUSION:
    ALL_TARGETS += expand(
        logpath("{region}", "exclusion_{technology}_{scenario}.done"),
        region=regions,
        technology=technologies,
        scenario=scenario,
    )

if DO_SUITABILITY:
    ALL_TARGETS += expand(
        logpath("{region}", "suitability_{technology}_{scenario}.done"),
        region=regions,
        technology=technologies,
        scenario=scenario,
    )


if DO_WEATHER_DATA_PREP:
    ALL_TARGETS += expand(
        logpath("{region}", "weather_data_prep_{weather_year}.done"),
        region=regions,
        weather_year=weather_years,
    )

if DO_WEATHER_BIAS_ADJUST:
    ALL_TARGETS += expand(
        logpath("{region}", "weather_bias_adjust.done"),
        region=regions,
    )

if DO_ENERGY_PROFILES:
    ALL_TARGETS += expand(
        logpath("{region}", "energy_profiles_{technology}_{weather_year}_{scenario}.done"),
        region=regions,
        scenario=scenario,
        technology=technologies,
        weather_year=weather_years,
    )

rule all:
    input:
        ALL_TARGETS